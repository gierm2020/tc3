<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="Director" Id="{7ba3b411-03e2-4744-bc96-6b241362c795}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Director
VAR_INPUT
    Cancela: BOOL; (**)
    Continua: BOOL; (**)
    Emergencia: BOOL;
    Fallo: BOOL; (**)
    Finalizada: BOOL;
    FinTarea: BOOL;
    FinCiclo: BOOL;
    Manual: BOOL;
    Marcha: BOOL;
    Para: BOOL; (* Detiene la produccion a final del ciclo *)
    Preparada: BOOL;
    Reinicia: BOOL;
    Restaurada: BOOL;
END_VAR
VAR_OUTPUT
    Modo: E_GEMMA := E_GEMMA.A6;
    Defecto: BOOL; (* Procedimientos de defecto *)
    Funcionamiento: BOOL; (* Procedimientos de funcionamiento*)
    Parada: BOOL; (* Procedimientos de parada *)
    Pausa: BOOL;
    Produccion: BOOL; (* Estacion en produccion *)
END_VAR
VAR
    Previo: E_GEMMA; (* Modo anterior *)
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* PARAMETROS DE SALIDA *)
Defecto := (Modo >= E_GEMMA.D1) AND (Modo <= E_GEMMA.D3);
Funcionamiento:= (Modo >= E_GEMMA.F1) AND (Modo <= E_GEMMA.F6);
Parada := (Modo >= E_GEMMA.A1) AND (Modo <= E_GEMMA.A7);

Pausa := (Modo = E_GEMMA.A3) OR (Modo = E_GEMMA.A4) OR (Modo = E_GEMMA.A5)
    OR (Modo = E_GEMMA.D1) OR (Modo = E_GEMMA.D2);
Produccion := (Modo = E_GEMMA.A2) OR (Modo = E_GEMMA.A3) OR (Modo = E_GEMMA.F1)
    OR (Modo = E_GEMMA.F2) OR (Modo = E_GEMMA.F3) OR (Modo = E_GEMMA.F5)
    OR (Modo = E_GEMMA.F6) OR (Modo = E_GEMMA.D3);

(* FUNCION DE ESTADO *)
IF Emergencia THEN
    Modo := E_GEMMA.D1;
ELSIF Reinicia THEN
    Modo := E_GEMMA.A6;
ELSE
    CASE Modo OF
        (* Procedimientos de parada *)
        E_GEMMA.A1: (* Parada en el estado inicial *)
            IF NOT Restaurada THEN
                Modo := E_GEMMA.A6;
            ELSIF Manual THEN
                Modo := E_GEMMA.F4;
            ELSIF Marcha THEN
                Modo := E_GEMMA.F2;
            END_IF
        E_GEMMA.A2: (* Parada solicitada a final de ciclo *)
            IF Fallo THEN
                Previo := E_GEMMA.A2;
                Modo := E_GEMMA.D2;
            ELSIF FinCiclo THEN
                Modo := E_GEMMA.A1;
            ELSE
                (* En este caso no es necesario que suceda nada! *)
                ;
            END_IF
        E_GEMMA.A3: (* Parada solicitada en un estado determinado *)
            ;
        E_GEMMA.A4: (* Parada obtenida *)
            ;
        E_GEMMA.A5: (* Preparando de reanudacion tras el fallo *)
            IF Continua THEN
                Modo := Previo;
            ELSIF Cancela THEN
                Modo := E_GEMMA.A6;
            END_IF
        E_GEMMA.A6: (* Situando la PO en el estado inicial *)
            IF Restaurada THEN
                Modo := E_GEMMA.A1;
            ELSIF Manual THEN
                Modo := E_GEMMA.F4;
            END_IF
        E_GEMMA.A7: (* Situando la PO en un estado determinado *)
            ;

        (* Procedimientos de funcionamiento *)
        E_GEMMA.F1: (* Produccion normal *)
            IF Fallo THEN
                Previo := E_GEMMA.A2;
                Modo := E_GEMMA.D2;
            ELSIF FinTarea THEN
                Modo := E_GEMMA.F3;
            ELSIF Para THEN
                Modo := E_GEMMA.A2;
            END_IF
        E_GEMMA.F2: (* Secuencia de preparacion *)
            IF Preparada THEN
                Modo := E_GEMMA.F1;
            ELSE
                (* En este caso no es necesario que suceda nada! *)
                ;
            END_IF
        E_GEMMA.F3: (* Secuencia de finalizacion *)
            IF Finalizada THEN
                Modo := E_GEMMA.A1;
            ELSE
                (* En este caso no es necesario que suceda nada! *)
                ;
            END_IF
        E_GEMMA.F4: (* Verificacion sin orden *)
            IF Fallo THEN
                Previo := E_GEMMA.A2;
                Modo := E_GEMMA.D2;
            ELSIF NOT Manual THEN
                Modo := E_GEMMA.A6;
            ELSE
                (* En este caso no es necesario que suceda nada! *)
                ;
            END_IF
        E_GEMMA.F5: (* Verificacion en orden *)
            ;
        E_GEMMA.F6: (* Prueba *)
            ;

        (* Procedimientos de defecto *)
        E_GEMMA.D1: (* Parada de emergencia *)
            IF NOT Emergencia THEN
                Modo := E_GEMMA.A6;
            ELSE
                (* En este caso no es necesario que suceda nada! *)
                ;
            END_IF
        E_GEMMA.D2: (* Diagnostico/tratamiento de los fallos *)
            IF NOT Fallo THEN
                Modo := E_GEMMA.A5;
            ELSE
                (* En este caso no es necesario que suceda nada! *)
                ;
            END_IF

        E_GEMMA.D3: (* Produccion a pesar de los fallos *)
            ;
    END_CASE
END_IF]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>