<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="L6_Tapas" Id="{bb89ac45-4910-4319-9986-f6a3e6f8b97b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK L6_Tapas
VAR CONSTANT
	ColSecIndeterm	: DWORD := 16#80101010; // gris claro
	ColSecVacio		: DWORD := 16#80808080; // gris oscuro
	ColSecPendiente : DWORD := 16#80000080; // azul
	ColSecDescargar	: DWORD := 16#80008000; // verde
	ColSecEvacuar	: DWORD := 16#80800000; // rojo
END_VAR

VAR_INPUT
	ReqNumero	: DINT := 2; 		// requisito de número de tapas deseadas
	ReqAlta		: REAL := 18.5;  	// requisito de tapa alta
	ReqVacio	: REAL := 2.0; 		// si mide menos el sector está vacío

	ReqAluBaja	: BOOL := FALSE;
	ReqAluAlta	: BOOL := TRUE;
	ReqBlnBaja  : BOOL := TRUE;
    ReqBlnAlta  : BOOL := FALSE;
	ReqNgrBaja  : BOOL := FALSE;
    ReqNgrAlta  : BOOL := FALSE; 

	ResPend	  	: DINT := 0;
	ResReal		: DINT := 0;
	ResRech   	: DINT := 0;
END_VAR

VAR_OUTPUT
	FaltaMat	: BOOL; 
	ExeAlime	: BOOL;
	ExeCarga	: BOOL;
	ExeEvacu	: BOOL;
	ExeDesca	: BOOL;
	ExePlato	: BOOL;
END_VAR

VAR
END_VAR

VAR
	NumGiros 	: DINT := 0;
	PosCarga	: DINT;
	PosFotoe	: DINT;
	PosMedid	: DINT;
	PosInduc	: DINT;
	PosEvacu	: DINT;
	PosDesca	: DINT;

	angulo	 	: REAL := 0.0;
	anguloanim 	: REAL := 0.0; // angulo para animación que gira lentamente
	//HaGirado	: BOOL := FALSE;

	vEvacuar_Tapa		: ARRAY[0..7] OF BOOL;
	vDescargar_Tapa		: ARRAY[0..7] OF BOOL;
	
	vInicializando 		: ARRAY[0..7] OF BOOL;	
	//vCargar_Tapa		: ARRAY[0..7] OF BOOL;
	vTapa_Cargada 		: ARRAY[0..7] OF BOOL;
	vAltura_Medida		: ARRAY[0..7] OF BOOL;
	vAltura_Pulsos 		: ARRAY[0..7] OF DWORD;
	vAltura_Mm 			: ARRAY[0..7] OF REAL;
	vTapa_Alta			: ARRAY[0..7] OF BOOL;
	vClaridad_Medida	: ARRAY[0..7] OF BOOL;
	vTapa_Clara 		: ARRAY[0..7] OF BOOL;	
	vMateral_Medido		: ARRAY[0..7] OF BOOL;	
	vTapa_Metalica 		: ARRAY[0..7] OF BOOL;	
	vstrUi 				: ARRAY[0..7] OF STRING;
	vstrToolTip			: ARRAY[0..7] OF STRING;
	vColorSector		: ARRAY[0..7] OF DWORD;
	
	vColorFill			: ARRAY[0..7] OF DWORD;
	vColorFont			: ARRAY[0..7] OF DWORD;	
	vColorFrame			: ARRAY[0..7] OF DWORD;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="A1Inicializar" Id="{4758c6c6-4ea4-433c-a79f-8e413ab5f879}">
      <Declaration><![CDATA[METHOD A1Inicializar : BOOL

VAR_INPUT
	DebePreparar 	: BOOL := TRUE;
END_VAR

VAR
	i : DINT;
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[ReqAluBaja		:= TRUE;
ReqAluAlta		:= TRUE;
ReqBlnBaja  	:= TRUE;
ReqBlnAlta  	:= TRUE;
ReqNgrBaja  	:= TRUE;
ReqNgrAlta  	:= TRUE; 

ResPend	  		:= 0;
ResReal			:= 0;
ResRech   		:= 0;

NumGiros	 	:= 0;
FaltaMat		:= FALSE;
PosMedid 		:= (NumGiros+6) MOD 8;
PosFotoe 		:= (NumGiros+5) MOD 8;
PosInduc 		:= (NumGiros+4) MOD 8;
PosEvacu 		:= (NumGiros+2) MOD 8;
PosDesca		:= (NumGiros+1) MOD 8;
PosCarga		:= (NumGiros+0) MOD 8;

angulo	 		:= 0.0;
anguloanim 		:= 0.0; // angulo para animación que gira lentamente
//HaGirado		:= FALSE;
ExePlato		:= FALSE;


FOR i:=0 TO 7 DO
	IF DebePreparar THEN
		vInicializando[i] 	:= TRUE;
	ELSE
		Vaciar(i);
	END_IF
END_FOR
Actualizar();]]></ST>
      </Implementation>
    </Method>
    <Method Name="A2InicioCiclo" Id="{8319a516-a09e-4e02-8848-93e12a8ca075}">
      <Declaration><![CDATA[METHOD A2InicioCiclo : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ResPend 	:= ReqNumero - ResReal;
ExeAlime 	:= ResPend > 0;
ExeCarga	:= ExeAlime AND 
				NOT vInicializando[PosCarga] AND
				NOT vTapa_Cargada[PosCarga] AND
				NOT FaltaMat;
ExeEvacu	:= vEvacuar_Tapa[PosEvacu];
ExeDesca    := vDescargar_Tapa[PosDesca];
ExePlato	:= TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Actualizar" Id="{76d8c916-080e-414e-a1dc-0f3a274c86ba}">
      <Declaration><![CDATA[METHOD Actualizar : BOOL
VAR_INPUT
END_VAR

VAR
	i 	: INT := 0;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
FOR i:=0 TO 7 DO
	vstrTooltip[i] := CONCAT('Sector ', TO_STRING(i));
	vstrTooltip[i] := CONCAT(vstrTooltip[i], ' :');
	IF vInicializando[i] THEN
		vAltura_Medida[i]	:= FALSE;
		vAltura_Pulsos[i]	:= 0;
		vAltura_Mm[i] 		:= 0;
	
		vClaridad_Medida[i]	:= FALSE;
		vTapa_Clara[i] 		:= FALSE;
	
		vMateral_Medido[i]	:= FALSE;	
		vTapa_Metalica[i] 	:= FALSE;
	
		//vCargar_Tapa[i]		:= FALSE;
		vEvacuar_Tapa[i]	:= FALSE;
		vDescargar_Tapa[i]	:= FALSE;	
		vColorSector[i] 	:= ColSecIndeterm;
		vstrUi[i] 			:= '?';
		vstrTooltip[i] 		:= CONCAT(vstrTooltip[i], ' Contenido desconocido => Medir');
		
	ELSIF NOT vTapa_Cargada[i] THEN
		//vCargar_Tapa[i]		:= TRUE;
		vEvacuar_Tapa[i]	:= FALSE;
		vDescargar_Tapa[i]	:= FALSE;	
		vColorSector[i] 	:= ColSecVacio;
		vstrUi[i] 			:= '___';
		vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], ' Sector vacío => Cargar');

	ELSE
		IF vAltura_Medida[i] THEN //  AND vTapa_Cargada[i] redundante porque se cumple
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], SEL(vTapa_Alta[i], ' Baja (', ' Alta ('));
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], TO_STRING(vAltura_Pulsos[i]));
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], ' pulsos, ');
			vstrTooltip[i]	:= CONCAT(vstrTooltip[i], TO_STRING(vAltura_Mm[i]));
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], ' mm);');
		ELSE
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], ' ¿altura?;');
		END_IF
		
		IF vClaridad_Medida[i] THEN
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], SEL(vTapa_Clara[i], ' oscura;', ' clara;'));
		ELSE
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], ' ¿claridad?;');							
		END_IF
		IF vMateral_Medido[i] THEN
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], SEL(vTapa_Metalica[i], ' plástico;', ' metal;'));
		ELSE
			vstrTooltip[i] 	:= CONCAT(vstrTooltip[i], ' ¿material?;');							
		END_IF
		
		IF vAltura_Medida[i] AND vClaridad_Medida[i] AND vMateral_Medido[i] THEN
			vDescargar_Tapa[i] := (
					(ReqAluAlta AND vTapa_Metalica[i] AND vTapa_Alta[i]) OR
					(ReqAluBaja AND vTapa_Metalica[i] AND NOT vTapa_Alta[i]) OR
					(ReqBlnAlta AND NOT vTapa_Metalica[i] AND vTapa_Clara[i]  AND vTapa_Alta[i]) OR
					(ReqBlnBaja AND NOT vTapa_Metalica[i] AND vTapa_Clara[i]  AND NOT vTapa_Alta[i]) OR
					(ReqNgrAlta AND NOT vTapa_Metalica[i] AND NOT vTapa_Clara[i]  AND vTapa_Alta[i]) OR
					(ReqNgrBaja AND NOT vTapa_Metalica[i] AND NOT vTapa_Clara[i]  AND NOT vTapa_Alta[i]));
			vEvacuar_Tapa[i] := NOT vDescargar_Tapa[i];
			vColorSector[i] := SEL(vDescargar_Tapa[i],ColSecEvacuar, ColSecDescargar);
			vstrTooltip[i] := CONCAT(vstrTooltip[i], SEL(vDescargar_Tapa[i], ' => Evacuar', '=> Descargar'));
		ELSE
			vDescargar_Tapa[i]	:= FALSE;
			vEvacuar_Tapa[i]	:= FALSE;
			vColorSector[i] 	:= ColSecPendiente; 			
			vstrTooltip[i] 		:= CONCAT(vstrTooltip[i], '=> Esperar');
		END_IF 
		vstrUi[i] := SEL(vAltura_Medida[i],'?', SEL(vTapa_Alta[i], 'B','A'));
		vstrUi[i] := CONCAT(vstrUi[i], SEL(vClaridad_Medida[i], '?',SEL(vTapa_Clara[i],'O','C')));
		vstrUi[i] := CONCAT(vstrUi[i], SEL(vMateral_Medido[i], '?', SEL(vTapa_Metalica[i], 'P','M'))); 		
	END_IF
	
	IF vInicializando[i] THEN								// caso A ?
		vColorFill[i]		:= colDesFill;
		vColorFont[i]		:= colDesFont;
		vColorFrame[i]		:= colDesFrameSin;
	ELSIF NOT vTapa_Cargada[i] THEN							// Caso B ___
		vColorFill[i]		:= colVacFill;
		vColorFont[i]		:= colVacFont;
		vColorFrame[i]		:= colVacFrameSin;
	ELSIF NOT vClaridad_Medida[i] THEN						// Caso C A??
		vColorFill[i]		:= colTapFill;
		vColorFont[i]		:= colTapFont;
		vColorFrame[i]		:= colTapFrameSin;
	ELSIF vTapa_Clara[i] AND NOT vMateral_Medido[i] THEN	// Caso D AC?
		vColorFill[i]		:= colClaFill;
		vColorFont[i]		:= colClaFont;
		vColorFrame[i]		:= colClaFrameSin;
	ELSIF NOT vTapa_Clara[i] AND NOT vMateral_Medido[i] THEN // Caso E AO?
		vColorFill[i]		:= colClaFill;
		vColorFont[i]		:= colClaFont;
		vColorFrame[i]		:= colClaFrameSin;
	ELSIF vTapa_Clara[i] AND vTapa_Metalica[i] THEN 		// Caso F ACM
		vColorFill[i]		:= colClaFill;
		vColorFont[i]		:= colClaFont;
		vColorFrame[i]		:= colClaFrameSin;
	ELSIF vTapa_Clara[i] AND NOT vTapa_Metalica[i] THEN 	// Caso G ACP
		vColorFill[i]		:= colBlnFill;
		vColorFont[i]		:= colBlnFont;
		vColorFrame[i]		:= colBlnFrameSin;
	ELSIF NOT vTapa_Clara[i] AND NOT vTapa_Metalica[i] THEN // Caso G AOP
		vColorFill[i]		:= colNgrFill;
		vColorFont[i]		:= colNgrFont;
		vColorFrame[i]		:= colNgrFrameSin;
	ELSE
		vColorFill[i]		:= colErrFill;
		vColorFont[i]		:= colErrFont;
		vColorFrame[i]		:= colErrFrameSin;		
	END_IF
	IF vDescargar_Tapa[i] THEN
		vColorFrame[i]		:= colAluFrameOk; // se aplica el mismo independientemente del tipo de tapa!	
	END_IF
	IF vEvacuar_Tapa[i] THEN
		vColorFrame[i]		:= colAluFrameKo; // se aplica el mismo independientemente del tipo de tapa!			
	END_IF
END_FOR 
ResPend := ReqNumero - ResReal;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Animar" Id="{de346600-d46e-4dc7-b63f-da26d1696097}">
      <Declaration><![CDATA[METHOD Animar : BOOL
VAR_INPUT
	PlatoActivo : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF ExePlato AND PlatoActivo AND anguloanim >-35.0 THEN
	anguloanim := anguloanim - 0.2;
ELSIF NOT PlatoActivo THEN
	anguloanim := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="B1PostAlime" Id="{68228dc4-db57-4234-af5a-76549516de52}">
      <Declaration><![CDATA[METHOD B1PostAlime : BOOL
VAR_INPUT
	Done	: BOOL;
	FM		: BOOL;
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Done THEN
	FaltaMat := FM;
	ExeAlime := FALSE;
	//Actualizar();
END_IF
 ]]></ST>
      </Implementation>
    </Method>
    <Method Name="B2PostCarga" Id="{b17e1a0b-f1bf-4df1-8172-1b04521564f1}">
      <Declaration><![CDATA[METHOD B2PostCarga : BOOL
VAR_INPUT
	Done: BOOL; 	// el brazo de carga ha terminado
	Mover: BOOL; 	// el brazo de carga ha movido la tapa
END_VAR

VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Done AND ExeCarga THEN // Mover THEN
	ExeCarga					:= FALSE;
	vInicializando[PosCarga]	:= FALSE;		
	vTapa_Cargada[PosCarga]		:= TRUE;
	vAltura_Medida[PosCarga]	:= FALSE;
	vAltura_Pulsos[PosCarga]	:= 0;
	vAltura_Mm[PosCarga] 		:= 0;		
	vClaridad_Medida[PosCarga]	:= FALSE;
	vTapa_Clara[PosCarga] 		:= FALSE;		
	vMateral_Medido[PosCarga]	:= FALSE;	
	vTapa_Metalica[PosCarga] 	:= FALSE;
	//Actualizar();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="B3PostMedid" Id="{e0bd59f6-28b6-48d4-9b89-31b0758ec910}">
      <Declaration><![CDATA[METHOD B3PostMedid : BOOL
VAR_INPUT
	Altura_Mm			: REAL;
	Altura_Pulsos		: UDINT;	
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[vInicializando[PosMedid]	:= FALSE;	
IF Altura_Mm < ReqVacio THEN
	Vaciar(PosVacia:=PosMedid);
ELSE
	vTapa_Cargada[PosMedid]		:= TRUE;			
	vAltura_Medida[PosMedid]	:= TRUE;
	vAltura_Pulsos[PosMedid]	:= Altura_Pulsos;
	vAltura_Mm[PosMedid]		:= Altura_mm;
	vTapa_Alta[PosMedid]		:= Altura_mm > ReqAlta;
	//Actualizar();
END_IF	
]]></ST>
      </Implementation>
    </Method>
    <Method Name="B4PostFotoe" Id="{afe73e40-fb85-4efe-b906-85a197d79803}">
      <Declaration><![CDATA[METHOD B4PostFotoe : BOOL
VAR_INPUT
	Tapa_Clara: BOOL;
END_VAR

VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[vClaridad_Medida[PosFotoe]	:= TRUE;
vTapa_Clara[PosFotoe]		:= Tapa_Clara;
//Actualizar();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="B5PostInduc" Id="{88973f08-454f-49dc-aa0f-ae73f3546455}">
      <Declaration><![CDATA[METHOD B5PostInduc : BOOL
VAR_INPUT
	Tapa_Metalica: BOOL;
END_VAR

]]></Declaration>
      <Implementation>
        <ST><![CDATA[vMateral_Medido[PosInduc]	:= TRUE;
vTapa_Metalica[PosInduc]	:= Tapa_Metalica;
//Actualizar();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="B6PostEvacu" Id="{5caa088b-866e-4274-a9c7-1d9f312ea165}">
      <Declaration><![CDATA[METHOD B6PostEvacu : BOOL

VAR_INPUT
	Done	: BOOL;
	Evacuar	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Done AND ExeEvacu THEN
	ExeEvacu 		:= FALSE;
	ResRech 		:= ResRech + 1;
	Vaciar(PosVacia := PosEvacu);
	//Actualizar();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="B7PosDesca" Id="{8738750d-0f53-4448-9145-e43c2aec455f}">
      <Declaration><![CDATA[METHOD B7PosDesca : BOOL
VAR_INPUT
	Done	: BOOL;
	Mover	: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF done AND ExeDesca THEN
	ExeDesca 		:= FALSE;
	ResReal 		:= ResReal + 1;
	ResPend			:= ReqNumero - ResReal;
	Vaciar(PosVacia	:= PosDesca);	
	//Actualizar();
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="B8PostPlato" Id="{0ccf35a2-1876-4048-9c8b-4ff29b7fbdc7}">
      <Declaration><![CDATA[METHOD B8PostPlato : BOOL
VAR_INPUT
	Ready : BOOL;
	Execute: BOOL;
	Done: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF ExePlato AND Done THEN
	ExePlato := FALSE;
	NumGiros 	:= NumGiros + 1;
	PosMedid 	:= (NumGiros+6) MOD 8;
	PosFotoe 	:= (NumGiros+5) MOD 8;
	PosInduc 	:= (NumGiros+4) MOD 8;
	PosEvacu 	:= (NumGiros+2) MOD 8;
	PosDesca	:= (NumGiros+1) MOD 8;
	PosCarga	:= (NumGiros+0) MOD 8;
	anguloanim 	:= 0;
	angulo 		:= SEL(angulo=0, angulo-45.0, 315.0); // , angulo+45.0, 0);
	Actualizar();	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Vaciar" Id="{3cc88831-9ad2-4cbf-830e-7f9be896f2fe}">
      <Declaration><![CDATA[METHOD Vaciar : BOOL
VAR_INPUT
	PosVacia: DINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[vInicializando[PosVacia]	:= FALSE;	
vTapa_Cargada[PosVacia]		:= FALSE;	
vAltura_Medida[PosVacia]	:= FALSE;
vAltura_Pulsos[PosVacia]	:= 0;
vAltura_Mm [PosVacia]		:= 0;	
vClaridad_Medida[PosVacia]	:= FALSE;
vTapa_Clara[PosVacia] 		:= FALSE;
vMateral_Medido[PosVacia]	:= FALSE;	
vTapa_Metalica[PosVacia] 	:= FALSE;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>