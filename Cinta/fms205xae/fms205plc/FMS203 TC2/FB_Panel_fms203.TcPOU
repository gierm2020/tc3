<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_Panel_fms203" Id="{614be02c-cdca-4e2c-a3c5-22f8a5e70bed}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Panel_fms203
(*
Panel de operador de la estacion

Copyright (c) 2020 Abdelmajid Goumbaz Aananou & victortorreslopez

GNU General Public License

Historia de modificaciones
0.1; 2020/10/23, vtl: revision
0.0, 2020/10/16, AGA: creacion
*)
VAR_INPUT
    Modo: E_GEMMA;
    TR: TIME := T#3s; (* Tiempo de reinicio *)
END_VAR
VAR_OUTPUT
    Emergencia: BOOL;
    Manual: BOOL;
    Reinicia: BOOL; (* Restablece situacion inicial *)
    Restablece: BOOL; (* Restablece la condicion de fallo *)
    Restaura: BOOL; (* Restaura condiciones iniciales *)
    Marcha: BOOL;
    Para: BOOL;
END_VAR
VAR
    (* Bloques funcionales *)
    CLK: FB_Clock;
    FP_PM: R_TRIG;
    TmpReinicio: TON;

    (* Entradas *)
    PE AT %I*: BOOL; (* Pulsador de parada de emergencia [EMERGENCY STOP] *)
    PM AT %I*: BOOL; (* Pulsador de marcha [START] *)
    PS AT %I*: BOOL := TRUE; (* Pulsador de parada [STOP] *)
    SM AT %I*: BOOL; (* Selector de modo AUTO/MAN [AUTO/MAN] *)

    (* Salidas *)
    AS AT %Q*: BOOL; (* Avisador sonoro *)
    LA AT %Q*: BOOL; (* Lampara de alarma [ALARM] *)
    LM AT %Q*: BOOL; (* Lampara de marcha [START] *)
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* PARAMETROS DE SALIDA *)
Emergencia := PE;
Manual := SM;
Reinicia := TmpReinicio.Q;
Restablece := PM;
Restaura := PM;
Marcha := FP_PM.Q;
Para := NOT PS;

(* ACCIONES *)
AS := (Modo = F3) OR (((Modo = D1) OR (Modo = D2) OR (Modo = F2)) AND CLK.Q);
LA := ((Modo = A6) OR (Modo = D1) OR (Modo = D2)) AND CLK.Q;
LM := ((Modo = A2) OR (Modo = F1) OR (Modo = F2) OR (Modo = F3)) OR ((Modo = A1) AND CLK.Q);

(* BLOQUES FUNCIONALES *)
CLK();

FP_PM(CLK := PM);

TmpReinicio(
    IN := PM AND NOT PS,
    PT := TR
    );
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>